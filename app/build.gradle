apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion 9
        targetSdkVersion 22
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
    }

    /**utf-8编码*/
    compileOptions.encoding = "utf-8"

    /**java版本**/
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    /**解决lint报错*/
    lintOptions {
        abortOnError false
    }

    /**打包签名配置*/
    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD

        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false//混淆
            zipAlignEnabled true//对齐
            shrinkResources true//去除无用src
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为appName_{version}_{date}_storeName.apk
                        def fileName = "calc_v${defaultConfig.versionName}_${releaseTime()}_release_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    /** 友盟多渠道打包*/
    productFlavors {
        googlePlay {}
        wandoujia {}
//        _360 {}
//        baidu {}
        xiaomi {}
        tencent {}
//        taobao {}
//        uc {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.navasmdc:MaterialDesign:1.5@aar'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.umeng.analytics:analytics:latest.integration'

}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}